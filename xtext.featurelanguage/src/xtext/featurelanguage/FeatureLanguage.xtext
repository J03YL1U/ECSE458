grammar xtext.featurelanguage.FeatureLanguage with org.eclipse.xtext.common.Terminals

generate featureLanguage "http://www.featurelanguage.xtext/FeatureLanguage"

FeatureLanguage:
        (concepts+=Concept)*
        'constraints' (constraints+=Constraint)*
        'keys' (keys+=Key)*
        'properties' (properties+=Property)*
    	'features' (features+=Feature)*;
    	
NamedElement:
	Concept | Characteristic;
    	
Concept:
        'concept' name=ID
        (characteristics+=Characteristic)*;
       
Characteristic:
        /*(typeName=STRING)*/ (type=Type) name=ID ((multiplicity=MULTIPLICITY) | '{' (literals+=STRING)* '}')?;
        
Type:
	name=ID;

Condition:
        operator=(ComparisonOperator) (value=INT);
	        
Constraint:
	(concept=[Concept])('.'(characteristic=[Characteristic])) (condition=Condition) (errorMsg=STRING);
	
Key:
	(concept=[Concept])('.'(characteristic=[Characteristic])) (keyType=KeyType); //possibly add a 'unique (Concept)' to signify uniqueness in that concept only
	
ConceptProperty:
	(concept=[Concept]) isRoot?='root';

CharacteristicProperty:
	(concept=[Concept])('.'(characteristic=[Characteristic])) isLazy?='lazy';
	
Property:
	ConceptProperty | CharacteristicProperty;
       
Feature:
        (verb=Verb) (concept=[Concept])('.'(characteristic=[Characteristic]))?;

enum Verb:
        Add | Remove | Update | Display; 
        
enum KeyType:
	unique | autounique | index;  
	
enum ComparisonOperator:
	GreaterThan = '>' | GreaterThanOrEqual = '>=' | Equal = '=' | LessThanOrEqual = '<=' | LessThan = '<';
        
terminal MULTIPLICITY:
	(INT | '*')'..'(INT | '*');